BACKUP ~bgfaces/backup~
AUTHOR ~mod: polymorphedsquirrel | art: artastrophe~
README ~%MOD_FOLDER%/readme.html~
VERSION ~0.1~




LANGUAGE ~English~
         ~english~		 
         ~%MOD_FOLDER%/tra/english/setup.tra~
	
BEGIN @010 //init
INSTALL_BY_DEFAULT
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @000
	/** Flag requesting prompting the user to hand-pick between altrenative portrait versions. */
	OUTER_SET manual_alternatives = 1
	/** Flag requesting to display readme files showcasig available alternatives for each NPC where they exist. */
	OUTER_SET show_alternatives = 0
	
	OUTER_TEXT_SPRINT portrait_dir ~%USER_DIRECTORY%/Portraits~
	

	OUTER_TEXT_SPRINT any_game ~\(bgee\|sod\|bg2ee\)~
	
	OUTER_TEXT_SPRINT any_gender ~\(male\|female\|other\)~
	
	OUTER_TEXT_SPRINT playable_race ~\(male\|female\)~
	
	OUTER_TEXT_SPRINT any_npc_type ~\(canonnpcs\|modnpcs\|plotnpcs\|\minornpcs\)~
	/** Matches any dir/file name other than symbolic . and .. */
	OUTER_TEXT_SPRINT any_dir ~\([^\.\\/][^\\/]*\)\|\(\.[^\.\\/][^\\/]*\)\|\(\.\.[^\\/]+\)~
	
	//OUTER_TEXT_SPRINT npc_regex ~%MOD_FOLDER%/%any_npc_type%/%game_dirs%/%any_gender%/\([^/\\]+\)~

	
	
	//sets game_dirs to a regex alternative of included games, i.e. (bgee|sod|bg2ee) 3 EET
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN
		ACTION_IF GAME_INCLUDES ~sod~ THEN BEGIN
			OUTER_TEXT_SPRINT game_dirs ~\(bgee\|sod\)~
		END ELSE BEGIN
			OUTER_TEXT_SPRINT game_dirs ~\(bgee\)~
		END
	END ELSE BEGIN
		ACTION_IF GAME_IS ~bg2ee~ THEN BEGIN
			OUTER_TEXT_SPRINT game_dirs ~\(bg2ee\)~
		END ELSE BEGIN //EET
			ACTION_IF GAME_IS ~eet~ BEGIN
				OUTER_TEXT_SPRINT game_dirs ~\(bgee\|sod\|bg2ee\)~
			END
		END
	END
		
	
	/** Copy portrait files which path is covered by a sequence of regular expressions for individual directories.
     *	The directories on path serve as role/content/gender filter and thus subtries of the collection can be
	 *  excluded or included. Only the first argument is mandatory, the rest will match all subdirectories.
	 *  If you wish to copy portraits of NPCs regardless of their role, choose %any_npc_type% for npctype.
	 *  @returns array with paths of all copied files.
	 */
	DEFINE_ACTION_FUNCTION copy_npc_portraits
	INT_VAR
		/** Flag, which set, specifies that any file to be copied must exist in the game. */
		existingOnly = 0
	STR_VAR 
		/** Regexp for name of root directory with npc portraits (alternative from npcs|modnpcs|plotnpcs etc.)*/
		npctype = ~~
		/** Regexp for game directories to cover: bgee|sod|bg2ee */	
		games = ~~
		/** Regexp for gender directory grouping individual npcs. */
		gender = ~male\|female\|other~ 
		/** Regexp for npc directory (i.e. name/identifier). */
		name = ~~ //~[^/\\]+~
		/** Target directory into which all files are (flatly) copied. */
		target = ~override~
	RET_ARRAY
		paths
		alternatives
	BEGIN
	
		ACTION_IF (~%games%~ STRING_EQUAL ~~) THEN BEGIN 
			OUTER_TEXT_SPRINT games ~%game_dirs%~
		END
		ACTION_IF (~%name%~ STRING_EQUAL ~~) THEN BEGIN
			OUTER_TEXT_SPRINT name ~%any_dir%~
		END
		
		ACTION_IF existingOnly THEN BEGIN
			PRINT ~Overwrite portraits with files from %MOD_FOLDER%/%npctype%/%games%/%gender%/%name%/ => %target%~
		END ELSE BEGIN
			PRINT ~Copy portraits from %MOD_FOLDER%/%npctype%/%games%/%gender%/%name%/ to %target%~
		END
		
		ACTION_DEFINE_ASSOCIATIVE_ARRAY paths BEGIN END
		ACTION_DEFINE_ASSOCIATIVE_ARRAY alteratives BEGIN END
		
		OUTER_SET npccount = 0
		GET_DIRECTORY_ARRAY roots ~%MOD_FOLDER%~ ~^\(%npctype%\)$~ 
		
		ACTION_PHP_EACH roots AS _ => root BEGIN
			LOG ~%root%:~
			GET_DIRECTORY_ARRAY gamedirs ~%root%~ ~^\(%games%\)$~	
			
			ACTION_PHP_EACH gamedirs AS _ => game BEGIN 
				LOG ~  %game%:~
				GET_DIRECTORY_ARRAY sexes ~%game%~ ~^\(%gender%\)$~
				
				ACTION_PHP_EACH sexes AS _ => sex BEGIN
					LOG ~    %sex%:~
					GET_DIRECTORY_ARRAY names ~%sex%~ ~^\(%name%\)$~
				
					ACTION_PHP_EACH names AS _ => npc BEGIN
						LOG ~      %npc%:~
						OUTER_INNER_PATCH_SAVE npcname ~%npc%~ BEGIN //unqalified directory name
							REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%sex%/~ ~~
						END
						
						ACTION_DEFINE_ARRAY portraits BEGIN END
						GET_FILE_ARRAY portraits ~%npc%~ ~^.+\.bmp$~ 
						OUTER_TEXT_SPRINT copied_files ~~
						
						ACTION_PHP_EACH portraits AS _ => portrait BEGIN //copy all new bmp files to override
							
							OUTER_INNER_PATCH_SAVE filename ~%portrait%~ BEGIN //unqualified file name
								REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%npc%/~ ~~
							END
							OUTER_INNER_PATCH_SAVE basename ~%filename%~ BEGIN//file name without extension
								REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~.bmp~ ~~
							END
							
							
							ACTION_IF (NOT existingOnly) OR (FILE_EXISTS_IN_GAME ~%filename%~) THEN BEGIN
								OUTER_TEXT_SPRINT copied_files ~%copied_files%/%basename%~
								//LOG ~        %portrait%~
								COPY ~%portrait%~ ~%target%~
								//read file with each line containing a creature updated with the current portrait
								COPY  ~%npc%/%basename%.2da~ ~%MOD_FOLDER%~
									COUNT_2DA_ROWS 1 creature_count
							
									FOR (i = 0; i < creature_count; ++i) BEGIN
										READ_2DA_ENTRY i 0 1 creature
										
										INNER_ACTION BEGIN
											COPY_EXISTING ~%creature%.cre~ ~override~
												WRITE_EVALUATED_ASCII 0x34 ~%basename%~ #8 
											IF_EXISTS BUT_ONLY_IF_IT_CHANGES
										END									
									END
								IF_EXISTS 
								ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%basename%.2da~ THEN BEGIN
									DELETE ~%MOD_FOLDER%/%basename%.2da~ //inside an if to shut up logs
								END
									
							END ELSE BEGIN
								LOG ~        skip %filename%~ 
							END
						END
						
						OUTER_SET npccount += 1
						ACTION_DEFINE_ASSOCIATIVE_ARRAY paths BEGIN ~%npc%~ => ~%copied_files%~ END
						ACTION_CLEAR_ARRAY portraits
						
				
						
						
						ACTION_IF DIRECTORY_EXISTS ~%npc%/alternative~ THEN BEGIN
							GET_DIRECTORY_ARRAY versions ~%npc%/alternative~ ~%any_dir%~
						END
						ACTION_DEFINE_ASSOCIATIVE_ARRAY alternatives BEGIN ~%npc%~ => versions END
						
						OUTER_SET altcount = 0
						ACTION_PHP_EACH versions AS _ => alt BEGIN							
							OUTER_SET altcount += 1
						END
						
						ACTION_IF manual_alternatives AND altcount > 1 AND NOT (~%copied_files%~ STRING_EQUAL ~~) THEN BEGIN
							OUTER_SPRINT prompt @043
							PRINT ~%prompt% %npcname%:~
						
							OUTER_SET altcount = 0 //print alternative portrait dirs
							ACTION_PHP_EACH versions AS _ => alt BEGIN
								OUTER_INNER_PATCH_SAVE altdesc ~%alt%~ BEGIN //unqalified directory name
									REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH ~%npc%/alternative/~ ~~
								END
								
								PRINT ~[%altcount%] %altdesc%~
								OUTER_SET altcount += 1
							END
							
							ACTION_IF show_alternatives AND FILE_EXISTS ~%npc%/alternatives.html~ THEN BEGIN
								AT_INTERACTIVE_NOW ~VIEW %npc%/alternatives.html~
							END
							
							OUTER_SET idx = altcount //read user choice into idx
							OUTER_WHILE (NOT IS_AN_INT %idx%) OR %idx% < 0 OR %idx% >= altcount BEGIN
								PRINT @044
								ACTION_READLN ~idx~
							END
								
							OUTER_SPRINT chosen $versions(~%idx%~)
							LOG ~%npcname% alternative %idx%: %chosen%~ 
							COPY_EXISTING ~%chosen%~ ~%target%~ IF_EXISTS BUT_ONLY_IF_IT_CHANGES
						END
						ACTION_CLEAR_ARRAY versions
						
					END
					ACTION_CLEAR_ARRAY names
				END
				ACTION_CLEAR_ARRAY sexes
			END
			ACTION_CLEAR_ARRAY gamedirs
		END
		ACTION_CLEAR_ARRAY roots
		
		ACTION_IF NOT npccount THEN BEGIN
			ACTION_DEFINE_ASSOCIATIVE_ARRAY paths BEGIN ~~ => ~~ END
			ACTION_DEFINE_ASSOCIATIVE_ARRAY alternatives BEGIN ~~ => ~~ END
		END
		
	END
	
	
	
	DEFINE_ACTION_FUNCTION update_portraits 
	STR_VAR creature = ~~
			medium = ~None~
	        large = ~None~
	RET 
		patched
	BEGIN
		OUTER_SET patched = 0
		ACTION_IF FILE_EXISTS_IN_GAME ~%creature%~ THEN BEGIN
			COPY_EXISTING ~%creature%~ ~override~
				WRITE_EVALUATED_ASCII 0x34 ~%medium%~ #8
				WRITE_EVALUATED_ASCII 0x3C ~%large%~ #8
			BUT_ONLY_IF_IT_CHANGES
			OUTER_SET patched = 1
		END
	END

	
	
	
BEGIN @041	//Turn off user prompt for alternatives
GROUP @040	//All components with portraits for NPCs
	//By default manual selection is on, so that automatic --install-all would become uninteractive
	PRINT @042
	OUTER_SET manual_alternatives = 0
	


BEGIN @050 //overwrite portraits of joinable NPCs
GROUP @040 //all NPC portraits
	LAF copy_npc_portraits STR_VAR npctype = ~canonnpcs~ END	

	
BEGIN @060 //overwrite portraits of mod NPCs 
GROUP @040 //all NPC portraits
	LAF copy_npc_portraits STR_VAR npctype = ~modnpcs~ END
	
	
	
BEGIN @070 //overwrite portraits of major plot npcs
GROUP @040 //all NPC portraits
//SUBCOMPONENT @070 //plot NPCs
	LAF copy_npc_portraits STR_VAR npctype = ~plotnpcs~ END
	
	
BEGIN @071 //add portraits to some minor npcs.
GROUP @040 //all NPC portraits
//SUBCOMPONENT @070 //plot NPCs
	LAF copy_npc_portraits STR_VAR npctype = ~minornpcs~ END
	

BEGIN @081 //overwrite portraits of NPCs from BG2/SoD 
GROUP @040 //all NPC portraits
REQUIRE_PREDICATE GAME_IS ~bgee~ @001
	LAF copy_npc_portraits STR_VAR npctype = ~canonnpcs~ games = ~sod\|bg2ee~ END
	

BEGIN @082
GROUP @040 //all NPC portraits
REQUIRE_PREDICATE GAME_INCLUDES ~sod~ @002
	LAF copy_npc_portraits STR_VAR npctype = ~canonnpcs~ games = ~bgee\|bg2ee~ END
	
BEGIN @083
GROUP @040 //all NPC portraits
REQUIRE_PREDICATE GAME_IS ~bg2ee~ @003
	LAF copy_npc_portraits STR_VAR npctype = ~canonnpcs~ games = ~bgee~ END

//todo: patch deder in bgee to use a halfling portrait




BEGIN @501 //Remove existing portraits from the selection screen
GROUP @500 //Portraits for PC
	COPY_EXISTING ~BGEE.LUA~ ~override~
		REPLACE_TEXTUALLY CASE_SENSITIVE EVALUATE_REGEXP ~^portraits =[^{]*{[^{}]*\({[^}]*}[^{}]*\)*}~ 
			~portraits = 
{
}~		

	
BEGIN @511 
GROUP @500
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @000	
	OUTER_TEXT_SPRINT pc_portraits ~%any_dir%~
	
	DEFINE_ACTION_FUNCTION list_pc_portraits
	STR_VAR
		group = ~charname~
		games = ~~
		gender = ~~
	RET
		portraits
	BEGIN
		ACTION_IF ~%games%~ STRING_EQUAL ~~ THEN BEGIN
			OUTER_TEXT_SPRINT games ~%any_dir%~
		END
		ACTION_MATCH ~%gender%~	WITH 
			~male~ ~Male~ ~MALE~ ~M~ ~m~ BEGIN
				OUTER_SET pc_gender = 1
			END
			~female~ ~Female~ ~FEMALE~ ~F~ ~f~ BEGIN
				OUTER_SET pc_gender = 2
			END
			DEFAULT
				FAIL ~Unrecognized gender: %gender%~
		END

		LAF copy_npc_portraits 
			STR_VAR 
				npctype	= EVALUATE_BUFFER ~%group%~ 
				games	= EVALUATE_BUFFER ~%games%~ 
				gender	= EVALUATE_BUFFER ~%gender%~ 
			RET_ARRAY copied=paths END
	
		OUTER_TEXT_SPRINT portraits ~~
	
	
		ACTION_PHP_EACH copied AS dir => files BEGIN 
			OUTER_INNER_PATCH_SAVE files ~%files%~ BEGIN
				REPLACE_EVALUATE CASE_SENSITIVE ~/\([^/\\]*\)~ BEGIN
				END ~	{'%MATCH1%', %pc_gender%},
~
			END
			OUTER_TEXT_SPRINT portraits ~%portraits%%files%~
		END
		OUTER_INNER_PATCH_SAVE portraits ~%portraits%~ BEGIN //remove trailing ','
			REPLACE_TEXTUALLY CASE_SENSITIVE EVALUATE_REGEXP ~,[^{}]*$~ ~~
		END		
	END
	
	LAF list_pc_portraits STR_VAR gender = ~male~ RET selection = portraits END
	LAF list_pc_portraits STR_VAR gender = ~female~ RET portraits END
	
	ACTION_IF ~%selection%~ STRING_EQUAL ~~ THEN BEGIN
		OUTER_TEXT_SPRINT selection ~%portraits%~
	END ELSE ACTION_IF NOT (~%portraits%~ STRING_EQUAL ~~) THEN BEGIN
		OUTER_TEXT_SPRINT selection ~%selection%,
%portraits%~
	END
	
	LOG ~Installing portrait set:~
	LOG ~%selection%~
	
	

	
	ACTION_IF NOT (~%selection%~ STRING_EQUAL ~~) THEN BEGIN
		LOG ~Patching BGEE.LUA; no files modified here signifies conflit with another module.~
		//Patch portrait selection list if it's not empty
		COPY_EXISTING ~BGEE.LUA~ ~override~ 
			REPLACE_TEXTUALLY CASE_SENSITIVE EVALUATE_REGEXP ~^portraits =[^{}]*{~
				~portraits = 
{
%selection%,~
		IF ~^portraits =[^{}]*{[^{}]*\({[^{}]*}[^{}]*\)+}~
		

		//Patch portrait selection list if it's empty
		COPY_EXISTING ~BGEE.LUA~ ~override~ //
			REPLACE_TEXTUALLY CASE_SENSITIVE EVALUATE_REGEXP ~^portraits =[^{}]*{[^{}]*}~ 
				~portraits =
{
%selection%
}~
		IF ~^portraits =[^{}]*{[^{}]*}~
	END
	
	
	
	
BEGIN ~Components for NPCs go first~
GROUP @040
DEPRECATED ~WeiDU hack~


BEGIN ~Components for PC go second~
GROUP @500
DEPRECATED ~WeiDU hack~
	